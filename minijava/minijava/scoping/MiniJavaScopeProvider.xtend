/*
 * generated by Xtext 2.10.0
 */
package org.xtext.projects.minijava.scoping

import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EReference
import org.eclipse.xtext.scoping.IScope
import org.eclipse.xtext.scoping.Scopes
import org.xtext.projects.minijava.miniJava.Block
import org.xtext.projects.minijava.miniJava.MethodDecl
import org.xtext.projects.minijava.miniJava.VarDecl
import org.xtext.projects.minijava.miniJava.MiniJavaPackage
import org.xtext.projects.minijava.miniJava.MemberSelection
import com.google.inject.Inject
import org.xtext.projects.minijava.MiniJavaModelUtil
import org.xtext.projects.minijava.MiniJavaTypeComputer
import org.xtext.projects.minijava.miniJava.CommonType

/**
 * This class contains custom scoping description.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#scoping
 * on how and when to use it.
 */
class MiniJavaScopeProvider extends AbstractMiniJavaScopeProvider {

	val epackage = MiniJavaPackage.eINSTANCE
	@Inject extension MiniJavaModelUtil
	@Inject extension MiniJavaTypeComputer

	override getScope(EObject context, EReference reference) {
		if (reference == epackage.getSymbolRef_Symbol()) {
			return scopeForSymbolRef(context)
		} else if (context instanceof MemberSelection) {
			return scopeForMemberSelection(context)
		}
		return super.getScope(context, reference)
	}

	def protected IScope scopeForSymbolRef(EObject context) {
		val container = context.eContainer
		return switch (container) {
			MethodDecl:
				Scopes.scopeFor(container.params)
			Block:
				Scopes.scopeFor(
					container.statements.takeWhile[it != context].filter(VarDecl),
					scopeForSymbolRef(container) // outer scope
				)
			default:
				scopeForSymbolRef(container)
		}
	}

	def protected IScope scopeForMemberSelection(MemberSelection sel) {
		if (sel.receiver.typeFor === null)
			return IScope.NULLSCOPE
		
		val type = (sel.receiver.typeFor as CommonType).classname

		if (type === null || type.isPrimitive)
			return IScope.NULLSCOPE

		val grouped = type.
			classHierarchyMembers.groupBy[it instanceof MethodDecl]
		val inheritedMethods = grouped.get(true) ?: emptyList
		val inheritedFields = grouped.get(false) ?: emptyList

		if (sel.methodinvocation) {
			return Scopes.scopeFor(
				type.methods + type.fields,
				Scopes.scopeFor(inheritedMethods + inheritedFields)
			)
		} else {
			return Scopes.scopeFor(
				type.fields + type.methods,
				Scopes.scopeFor(inheritedFields + inheritedMethods)
			)
		}
	}

}
